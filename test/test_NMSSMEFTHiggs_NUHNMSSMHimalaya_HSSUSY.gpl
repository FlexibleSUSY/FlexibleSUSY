# run:
# $ test/test_NMSSMEFTHiggs_NUHNMSSMHimalaya_HSSUSY.x
# $ gnuplot test/test_NMSSMEFTHiggs_NUHNMSSMHimalaya_HSSUSY.gpl

set terminal pdf enhanced size 10cm,10cm
set title 'tan({/Symbol b}) = 5, x_t = 0, {/Symbol l} = {/Symbol k} = 0.001, A_{/Symbol l} = A_{/Symbol k}, {/Symbol m} = M_S, m_A = M_S'
set logscale x
set grid
set key box bottom right
set xrange [0.1:10]
set yrange [80:125]
set xlabel 'M_S/TeV'
set ylabel 'M_h/GeV'
set encoding utf8
set minussign
set output 'MS-Mh.pdf'

datafile = 'test/test_NMSSMEFTHiggs_NUHNMSSMHimalaya_HSSUSY.dat'

set ylabel 'M_h^{1L}/GeV'

plot datafile using ($1/1000):2  t 'FO'   with lines dt 4 lc rgb 'web-green', \
     datafile using ($1/1000):3  t 'EFT'  with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):4  t 'FEFT' with lines dt 1 lc rgb 'red'

set ylabel 'M_h^{2L}/GeV'

plot datafile using ($1/1000):5  t 'FO'   with lines dt 4 lc rgb 'web-green', \
     datafile using ($1/1000):6  t 'EFT'  with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):7  t 'FEFT' with lines dt 1 lc rgb 'red'

set ylabel 'M_h^{3L}/GeV'

plot datafile using ($1/1000):8  t 'FO'   with lines dt 4 lc rgb 'web-green', \
     datafile using ($1/1000):9  t 'EFT'  with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):10 t 'FEFT' with lines dt 1 lc rgb 'red'

set ylabel '[M_h(FEFT) - M_h(FO)]/GeV'
set key box top left
unset yrange

plot datafile using ($1/1000):($4-$2)  t '1L' with lines dt 4 lc rgb 'web-green', \
     datafile using ($1/1000):($7-$5)  t '2L' with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):($10-$8) t '3L' with lines dt 1 lc rgb 'red'

set ylabel '[M_h(FEFT) - M_h(EFT)]/GeV'
set key box top right

plot datafile using ($1/1000):($4-$3)  t '1L' with lines dt 4 lc rgb 'web-green', \
     datafile using ($1/1000):($7-$6)  t '2L' with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):($10-$9) t '3L' with lines dt 1 lc rgb 'red'
