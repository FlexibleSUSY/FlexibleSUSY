# run:
# $ test/test_NMSSMEFTHiggs_NUHNMSSMHimalaya.x
# $ gnuplot test/test_NMSSMEFTHiggs_NUHNMSSMHimalaya.gpl

set terminal pdf enhanced size 10cm,13cm
set title 'tan({/Symbol b}) = 5, {/Symbol l} = {/Symbol k} = 0.001, A_{/Symbol l} = A_{/Symbol k}, {/Symbol m} = M_S, m_A = M_S'
set logscale x
set grid
set key box bottom right
set xrange [0.1:10]
set output 'MS-Mh.pdf'

set multiplot layout 2,1
set lmargin 10
set origin 0,0.3
set size 1,0.7

datafile = 'test/test_NMSSMEFTHiggs_NUHNMSSMHimalaya.dat'

set ylabel 'M_h/GeV'
unset xlabel
set format x ''

plot datafile using ($1/1000):2 t 'FO(1L)'   with lines dt 2 lc rgb 'web-green', \
     datafile using ($1/1000):3 t 'FEFT(1L)' with lines dt 1 lc rgb 'web-green', \
     datafile using ($1/1000):4 t 'FO(2L)'   with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):5 t 'FEFT(2L)' with lines dt 1 lc rgb 'blue', \
     datafile using ($1/1000):6 t 'FO(3L)'   with lines dt 2 lc rgb 'red', \
     datafile using ($1/1000):7 t 'FEFT(3L)' with lines dt 1 lc rgb 'red', \

set origin 0,0
set size 1,0.3
unset title

set yrange [-2:3]
set ytics 1
set xlabel 'M_S/TeV'
set ylabel '{/Symbol D}M_h/GeV'
set format x
set xtics

plot datafile using ($1/1000):($3-$2) t 'FEFT(1L)-FO(1L)' with lines dt 3 lc rgb 'web-green', \
     datafile using ($1/1000):($5-$4) t 'FEFT(2L)-FO(2L)' with lines dt 2 lc rgb 'blue', \
     datafile using ($1/1000):($7-$6) t 'FEFT(3L)-FO(3L)' with lines dt 1 lc rgb 'red'

set output
